load("@rules_cc//cc:defs.bzl", "cc_library")

# Google Mock.
cc_library(
    name = "googlemock",
    srcs = glob(
        include = [
            "src/*.cc",
            "src/*.h",
        ],
        exclude = [
            "src/gmock-all.cc",
            "src/gmock_main.cc",
        ],
    ),
    hdrs = glob([
        "include/**/*.h",
    ]),
    copts = select({
        "//:windows": [],
        "//conditions:default": ["-pthread"],
    }),
    defines = select({
        "//:has_absl": ["GTEST_HAS_ABSL=1"],
        "//conditions:default": [],
    }),
    features = select({
        "//:windows": ["windows_export_all_symbols"],
        "//conditions:default": [],
    }),
    linkopts = select({
        "//:windows": [],
        "//conditions:default": ["-pthread"],
    }),
    strip_include_prefix = "include",
    visibility = ["//visibility:public"],
    deps = [
        "//googletest",
    ] + select({
        "//:has_absl": [
            "@com_google_absl//absl/debugging:failure_signal_handler",
            "@com_google_absl//absl/debugging:stacktrace",
            "@com_google_absl//absl/debugging:symbolize",
            "@com_google_absl//absl/strings",
            "@com_google_absl//absl/types:optional",
            "@com_google_absl//absl/types:variant",
        ],
        "//conditions:default": [],
    }),
)

cc_library(
    name = "main",
    srcs = [
        "src/gmock_main.cc",
    ],
    features = select({
        "//:windows": ["windows_export_all_symbols"],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
    deps = [
        ":googlemock",
    ],
)
